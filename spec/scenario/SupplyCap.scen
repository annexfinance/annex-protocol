Test "Attempt to supply over set cap BEP20"
    NewComptroller price:1.0
    NewAToken ZRX aZRX
    Comptroller SetMarketSupplyCaps (aZRX) (0.5e18)
    Assert Equal (Comptroller SupplyCaps aZRX) (Exactly 0.5e18)
    Support aZRX collateralFactor:0.5
    Prep Geoff 0.5e18 ZRX aZRX
    AllowFailures
    Mint Geoff 0.5e18 aZRX
    Assert Revert
    Assert Equal (Bep20 ZRX TokenBalance Geoff) (Exactly 0.5e18)
    Assert Equal (Bep20 ZRX TokenBalance aZRX) (Exactly 0)

Test "Attempt to supply at set cap BEP20"
    NewComptroller price:1.0
    NewAToken ZRX aZRX
    NewAToken BAT aBAT
    Comptroller SetMarketSupplyCaps (aZRX) (100000000000000000001)
    Support aZRX collateralFactor:0.5
    Prep Geoff Some ZRX aZRX
    Mint Geoff 100e18 aZRX
    Assert Equal (Bep20 ZRX TokenBalance Geoff) (Exactly 0)
    Assert Equal (Bep20 ZRX TokenBalance aZRX) (Exactly 100e18)

Test "Attempt to supply below set cap BEP20"
    NewComptroller price:1.0
    NewAToken ZRX aZRX
    Comptroller SetMarketSupplyCaps (aZRX) (1e18)
    Support aZRX collateralFactor:0.5
    Prep Geoff Some ZRX aZRX
    Mint Geoff 0.5e18 aZRX
    Assert Equal (Bep20 ZRX TokenBalance Geoff) (Exactly 99.5e18)
    Assert Equal (Bep20 ZRX TokenBalance aZRX) (Exactly 0.5e18)

Test "Cannot supply more even all underlying is borrowed"
    NewComptroller price:1.0
    NewAToken ZRX aZRX
    NewAToken BAT aBAT
    Support aZRX collateralFactor:0.5
    Support aBAT collateralFactor:0.5
    Comptroller SetMarketSupplyCaps (aZRX) (1e18)
    Give aZRX 1e18 ZRX -- Faucet some zrx to borrow
    Prep Robert Some BAT aBAT
    Mint Robert 100e18 aBAT
    EnterMarkets Robert aBAT
    Borrow Robert 1e18 aZRX -- Robert borrows all ZRX
    Assert Equal (Bep20 ZRX TokenBalance aZRX) (Exactly 0)
    Prep Geoff Some ZRX aZRX
    AllowFailures
    Mint Geoff 1 aZRX
    Assert Revert
    Assert Equal (Bep20 ZRX TokenBalance Geoff) (Exactly Some)

Test "Setting supply cap restricted to admin"
    NewComptroller price:1.0
    ListedAToken ZRX aZRX
    SetCollateralFactor aZRX collateralFactor:0.5
    AllowFailures
    From Robert (Comptroller SetMarketSupplyCaps (aZRX) (0.01e18))
    Assert Revert

Test "Supply cap guardian can set supply caps"
    NewComptroller price:1.0
    ListedAToken ZRX aZRX
    SetCollateralFactor aZRX collateralFactor:0.5
    Comptroller SetSupplyCapGuardian Geoff
    From Geoff (Comptroller SetMarketSupplyCaps (aZRX) (0.5e18))
    Assert Equal (Comptroller SupplyCaps aZRX) (Exactly 0.5e18)
    Assert Equal (Comptroller SupplyCapGuardian) (User Geoff Address)
    AllowFailures
    From Robert (Comptroller SetMarketSupplyCaps (aZRX) (0.01e18)) -- Robert still can't...
    Assert Revert
    From Robert (Comptroller SetMarketSupplyCaps (aZRX) (0.01e18))
    Assert Revert

Test "Only admin can set Supply Cap Guardian"
    NewComptroller price:1.0
    AllowFailures
    From Robert (Comptroller SetSupplyCapGuardian Robert) -- Robert has really gone rogue
    Assert Revert

Test "Reserves should not affect supply cap"
    NewComptroller price:1.0
    NewAToken USDC aUSDC
    Support aUSDC collateralFactor:0.5
    Prep Geoff Some USDC aUSDC
    Mint Geoff 14e18 aUSDC
    AddReserves 1e18 aUSDC Geoff
    Assert Equal (Bep20 USDC TokenBalance aUSDC) (Exactly 15e18)
    Assert Equal (AToken aUSDC Reserves) (Exactly 1e18)
    -- Current supply level should exclude reserves, which should be 15e18 - 1e18 = 14e18.
    --   Setting supply caps to 14e18 should block users from supplying.
    Comptroller SetMarketSupplyCaps (aUSDC) (14e18)
    AllowFailures
    Mint Geoff 1 aUSDC
    Assert Revert
    Successfully
    Comptroller SetMarketSupplyCaps (aUSDC) (15e18)
    Mint Geoff 999999999999999999 aUSDC

Test "SetBorrowCaps works correctly too"
    NewComptroller price:1.0
    NewAToken ZRX aZRX
    NewAToken BAT aBAT
    NewAToken USDC aUSDC
    Comptroller SetMarketBorrowCaps (aBAT aUSDC) (0.5e18 1000001)
    Assert Equal (Comptroller BorrowCaps aBAT) (Exactly 0.5e18)
    Assert Equal (Comptroller BorrowCaps aUSDC) (Exactly 1000001)
    Give aBAT 10e18 BAT -- Faucet some bat to borrow
    Give aUSDC 20e6 USDC
    Support aZRX collateralFactor:0.5
    Support aBAT collateralFactor:0.5
    Support aUSDC collateralFactor:0.5
    Prep Geoff Some ZRX aZRX
    Mint Geoff 100e18 aZRX
    EnterMarkets Geoff aZRX
    AllowFailures
    Borrow Geoff 1e18 aBAT
    Assert Revert
    Borrow Geoff 2e6 aUSDC
    Assert Revert
    Successfully
    Borrow Geoff 1e6 aUSDC
    Assert Equal (aToken aBAT BorrowBalance Geoff) (Exactly 0)
    Assert Equal (Bep20 BAT TokenBalance Geoff) (Exactly 0)
    Assert Equal (Bep20 BAT TokenBalance aBAT) (Exactly 10e18)
    Assert Equal (Bep20 USDC TokenBalance Geoff) (Exactly 1e6)
